plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.yfblock'
version '1.0.10'

ext {
    gitUrl  = 'https://github.com/yfblock/yfSql'
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/public/' }
    mavenLocal()
    mavenCentral()
}

// 设置多版本兼容
sourceSets {
    java11 {
        java {
            srcDirs = ['src/main/java11']
        }
    }
}

dependencies {
    // 多版本兼容文件
    implementation files("${System.properties['java.home']}/../lib/tools.jar")

    System.properties['java.home']

    java11Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testAnnotationProcessor project(":")

    testImplementation 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
    testImplementation 'mysql:mysql-connector-java:8.0.29'
    testImplementation 'org.xerial:sqlite-jdbc:3.36.0.3'
}


// 默认编译
compileJava {
    sourceCompatibility '1.8'
    targetCompatibility '1.8'
}

// 多版本之一11编译
compileJava11Java {
    options.compilerArgs << '--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED' <<
            '--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED' <<
            '--add-exports=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED' <<
            '--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'

    sourceCompatibility '11'
    targetCompatibility '11'
}

// 设置打包为Jar包
jar {
    into('META-INF/versions/11') {
        from sourceSets.java11.output
    }

    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Multi-Release": true
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption("charset", "UTF-8")
    options.addStringOption("-add-exports", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED")
    options.addStringOption("-add-exports ", "jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED")
    options.addStringOption("-add-exports  ", "jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED")
    options.addStringOption("-add-exports   ", "jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            version = rootProject.version
            from components.java
            pom {
                name = rootProject.name
                description = 'yfSql'
                packaging = 'jar'
                url = project.ext.gitUrl
                properties = [
                    myProp: "value",
                    "prop.with.dots": "anotherValue"
                ]
                licenses {
                    license {
                        name = 'GPL-2.0'
                        url = 'https://gitee.com/yfblock/yfSql/raw/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'yufeng'
                        name = 'yufeng'
                        email = '321353225@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:yfblock/yfSql.git'
                    developerConnection = 'scm:git:ssh://github.com/yfblock/yfSql.git'
                    url = project.ext.gitUrl
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            }
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

